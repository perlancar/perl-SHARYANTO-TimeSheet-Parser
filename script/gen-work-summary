#!perl

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;

use DateTime;
use DateTime::Format::ISO8601;
use File::Slurper qw(read_text);
use Perinci::CmdLine::Any;
use String::Indent qw(indent);
use SHARYANTO::TimeSheet::Parser qw(total_daily_totals);
use Text::ANSITable;

our %SPEC;

$SPEC{gen_monthly_work_summary} = {
    v => 1.1,
    summary => 'Generate work summary for a date range',
    args => {
        file => {
            summary => 'Org document containing daily entries',
            schema => 'str*',
            #req => 1,
            pos => 0,
        },
        from => {
            schema => ['date*', 'x.perl.coerce_to' => 'DateTime'],
            req => 1,
        },
        to => {
            schema => ['date*', 'x.perl.coerce_to' => 'DateTime'],
            req => 1,
        },
    },
};
sub gen_monthly_work_summary {
    my %args = @_;

    my @text;

    my $d1 = $args{from};
    my $d2 = $args{to};

    #say "D:d1=$d1, d2=$d2";

    my $res = total_daily_totals(
        str => read_text($args{file} // "$ENV{HOME}/notes/daily.org"),
        min_date=>$d1->ymd, max_date=>$d2->ymd);

    push @text, "** [", $d2->ymd, "] (", $res->[2], ") monthly summary\n";

    my $bd = $res->[3]{"func.breakdowns"};
    my $proj_mins = $bd->{project_minutes};
    my @projs = sort {$proj_mins->{$b} <=> $proj_mins->{$a}} keys %$proj_mins;
    push @text, "- top 3 projects: ",
        join(", ", grep {defined} @projs[0..2]), "\n";

    my $act_mins = $bd->{activity_minutes};
    my @acts = sort {$act_mins->{$b} <=> $act_mins->{$a}} keys %$act_mins;

    my $t = Text::ANSITable->new(use_color=>0, use_box_chars=>0, use_utf8=>0);
    $t->border_style("Default::single_ascii");
    $t->columns(["project", "duration"]);
    $t->set_column_style(duration => align => "right");
    for (@projs) {
        $t->add_row([$_,
                     sprintf("%s (%.0f%%)",
                             $bd->{project_durations}{$_},
                             $bd->{project_durpcts}{$_},
                         )]);
    }
    push @text, "- breakdown per project:\n", indent("  ", $t->draw);

    $t = Text::ANSITable->new(use_color=>0, use_box_chars=>0, use_utf8=>0);
    $t->border_style("Default::single_ascii");
    $t->columns(["activity", "duration"]);
    $t->set_column_style(duration => align => "right");
    for (@acts) {
        $t->add_row([$_,
                     sprintf("%s (%.0f%%)",
                             $bd->{activity_durations}{$_},
                             $bd->{activity_durpcts}{$_},
                         )]);
    }
    push @text, "- breakdown per activity:\n", indent("  ", $t->draw);;

    [200, "OK", join("", @text)];
}

Perinci::CmdLine::Any->new(
    url => '/main/gen_monthly_work_summary',
)->run;

# ABSTRACT:
# PODNAME:
